apply from: 'setupDatabase.gradle'

dependencies {
    compile project(':model')
    testCompile 'junit:junit:4.11'
    runtime "com.h2database:h2:1.4.196"
}



test{
    exclude '**/*IntegTest.class'
    reports.html.destination = file("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
}

sourceSets {
     integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test ) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}


jacocoTestReport{
    executionData(test,integrationTest)
}



check.dependsOn integrationTest
