plugins{
    id "org.sonarqube" version "2.5"
}
apply from :'jelastic.gradle'
apply plugin: 'build-dashboard'
ext.projectIds = ['group':'com.arolitec.todo','version':'0.3']

allprojects{
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'eclipse'
    
    repositories{
        mavenCentral()
    }
}

subprojects{
    apply plugin:'java'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    apply plugin: 'jdepend'
    apply plugin: 'checkstyle'
    apply plugin: 'project-report'
    
    pmd {
        ignoreFailures=true
        ruleSets = [
            "java-basic",
            "java-design"
        ]
    }
    jdepend{
        ignoreFailures=true
    }
    jdependMain {
        reports {
            xml.enabled true
            text.enabled false
        }
    }
    findbugs{
        ignoreFailures=true
        effort = "max"
    }
    checkstyle{
        ignoreFailures=true
        configFile file("${projectDir}/../config/checkstyle/checkstyle.xml")
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            exceptionFormat 'full'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
            html.stylesheet resources.text.fromFile("${projectDir}/../config/xsl/findbugs-custom.xsl")
        }
    }
    
}

task wrapper(type: Wrapper){
    gradleVersion="4.1"
}
    


 
 

